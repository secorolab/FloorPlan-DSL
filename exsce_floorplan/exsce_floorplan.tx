import floorplan_polytope
import floorplan_geometry

/*
    Floorplan language for modeling the floorplan of indoor environments.
*/

/*
TODO:
- Wall thickness specification
- Doorway width
*/

FloorPlan:
    'Floor plan' ':' name=ID
        spaces += Space 
        boolean_operations*=BooleanOperation?
        wall_openings*=WallOpening?
        wall_removal*=WallRemoval?
;

Space:
    'Space' name=ID ':'
        'shape' ':' shape=Polytope
        'location' ':' 
            location=SpaceLocation
        ('wall thickness' ':' wall_thickness=CartesianUnit)?
        ('features' ':' 
            floor_features*=FloorFeature
        )?
        ('order:' order=INT)?
;

/* ------------------------------------------ */
// Wall 

//FromWall 
Wall:
    INT | "null"
;

FromFrameRef:
    world?="world" | ref=[Space]('.walls[' index=Wall ']')?
;

//ToWall
ToFrameRef:
    "this" ('.walls[' index=Wall ']')?
;

WallRef:
    ref=[Space] '.walls[' index=Wall ']'
;

WallThickness:
    "wall"
;

/* ------------------------------------------ */
// Location

SpaceLocation:
    'from:' _from=FromFrameRef
    'to:' to=ToFrameRef
    pose=PoseDescription
    //flags
    (aligned?="not aligned" spaced?="spaced")#
;

FloorLocation:
    'from:' _from=FromFrameRef
    pose=PoseDescription
;

WallLocation:
    'wall:' wall=WallRef
    pose=PoseDescription
;

/* ------------------------------------------ */
// WallOpening

WallOpening:
    Entryway|Window
;

Entryway:
    'entryway:'
    'in:' space_a=[Space] ('and' space_b=[Space])?
    'shape:' shape=Polytope 
    'location:' loc=WallLocation
;

Window:
    'window:' 
    'in:' space_a=[Space] ('and' space_b=[Space])?
    'shape:' shape=Polytope 
    'location:' loc=WallLocation
;

/* ------------------------------------------ */
// FloorFeature

FloorFeature:
    DividerWall | Column
;

DividerWall:
    'divider:'
    'shape:' shape=Polytope
    'location:' loc=FloorLocation
;

Column:
    'column:'
    'shape:' shape=Polytope
    'location:' loc=FloorLocation
;

/* ------------------------------------------ */
// Boolean operations and others

BooleanOperation:
    Intersection | Difference | Union
;

Intersection:
    'intersection:' space_a=[Space] ',' space_b=[Space]
;

Difference:
    'difference:' min=[Space] '-' sub=[Space]
;

Union:
    'union:' spaces+=[Space]['+']
;

WallRemoval:
    "remove wall:" wall=WallRef
;

/* ------------------------------------------ */
// special rule for comments.
Comment:
    /\/\/.*$/
;