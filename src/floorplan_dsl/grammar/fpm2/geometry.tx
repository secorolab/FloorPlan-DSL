import units
import variables

/* ------------------------------------------ */
// Syntax: Structural entities

PointCoordinate:
    '('x=LengthValue',' y=LengthValue(',' z=LengthValue)?')'
;

PositionVector:
    (('x'':' x=LengthValue)? ('y'':' y=LengthValue)? ('z'':' z=LengthValue)?)#[","]
;

// TODO Rename this alpha beta gamma?
EulerAngles:
    (('x'':' x=AngleValue)? ('y'':' y=AngleValue)? ('z'':' z=AngleValue)?)#[","]
;

// Semantics of structural entities. 
// These are especially usefl/needed to generate json-ld models
Point:
'point:' name=ID
;

Frame:
'frame:' name=ID origin=Point?
;

Translation:
PointCoordinate|PositionVector
;

Orientation:
EulerAngles
;

PositionCoordinate:
translation=Translation
of=[Point]
wrt=[Frame]
;

PoseCoordinate:
of=[Frame]
wrt=[Frame]
translation=Translation?
orientation=Orientation?
;

/* ------------------------------------------ */
// Syntax: Polytope

Polygon: Rectangle | Circle | SimplePolygon;

Rectangle:
    'Rectangle' 'width' '=' width=LengthValue',' ('length' '=' length=LengthValue|'height' '=' height=LengthValue)
    (coordinates*=PointCoordinate[','] points*=Point)? // Semantics
;

Circle:
    'Circle' 'radius' '=' (radius=LengthValue)
;


SimplePolygon:
    'Polygon' 'points'':''['
        coordinates*=PointCoordinate[',']
    ']'
    points*=Point? //Semantics
;

