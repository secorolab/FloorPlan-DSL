import floorplan_polytope
import floorplan_geometry

/*
    Floorplan language for modeling the floorplan of indoor environments.
*/

/*
TODO:
- Polar coordinates for Circle spaces
*/

FloorPlan:
    'Floor plan' ':' name = ID
        spaces += Space 
        boolean_operations *= BooleanOperation?
        wall_openings *= WallOpening?
        wall_removal *= WallRemoval?
        default = DefaultValues
;

Space:
    'Space' name=ID ':'
        'shape' ':' shape=Polytope
        'location' ':' 
            location=SpaceLocation
        ('wall thickness' ':' wall_thickness=CartesianUnit)?
        ('wall height' ':' wall_height=CartesianUnit)?
        ('features' ':' 
            floor_features*=FloorFeature
        )?
        ('order:' order=INT)?
;

DefaultValues:
    'Default values' ':'
        'Wall thickness' ':' wall_thickness = CartesianUnit
        'Wall height' ':' wall_height = CartesianUnit
;

FloorPlanReference:
    Space | FloorFeature | WallOpening
;

/* ------------------------------------------ */
// Wall 

//FromWall 
Wall:
    INT | "null"
;

FromFrameRef:
    world?="world" | ref=[Space]('.walls[' index=Wall ']')?
;

//ToWall
ToFrameRef:
    "this" ('.walls[' index=Wall ']')?
;

WallRef:
    ref=[Space] '.walls[' index=Wall ']'
;

WallThickness:
    "wall"
;

/* ------------------------------------------ */
// Location

SpaceLocation:
    'from:' from_frame=FromFrameRef
    'to:' to_frame=ToFrameRef
    'pose:'
        pose=PoseDescription
    //flags
    (aligned?="not aligned" spaced?="spaced")#
;

FloorLocation:
    'from:' from_frame=ToFrameRef
    'pose' ':' 
        pose=PoseDescription
;

/* ------------------------------------------ */
// WallOpening

WallOpening:
    (entryway?='Entryway' | window?='Window') name=ID ':'
    'in:' wall_a=WallRef ('and' wall_b=WallRef)?
        'shape:' shape=VerticalPolytope
        'pose:' 
            pose=PoseDescription
;

/* ------------------------------------------ */
// FloorFeature

FloorFeature:
    ( divider?='Divider' | column?='Column' ) name=ID ':'
        'shape:' shape=Polytope
        'height' ':' height=CartesianUnit
        location=FloorLocation
;

/* ------------------------------------------ */
// Boolean operations and others

BooleanOperation:
    Intersection | Difference | Union
;

Intersection:
    'intersection:' space_a=[Space] ',' space_b=[Space]
;

Difference:
    'difference:' min=[Space] '-' sub=[Space]
;

Union:
    'union:' spaces+=[Space]['+']
;

WallRemoval:
    "remove wall:" wall=WallRef
;

/* ------------------------------------------ */
// special rule for comments.
Comment:
    /\/\/.*$/
;