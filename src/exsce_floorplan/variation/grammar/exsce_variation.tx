reference exsce-floorplan-dsl as f

Model:
    import_uri=Import
    variations+=Vars
;

Vars:
    (Variation|VariableRef)
;

Variation:
    ref=[f.FloorPlanReference | FQN] ':' '{'
        attributes += Attribute
    '}'
;

Attribute:
    fqn=FQN ':' distribution=Distribution
;

VariableRef:
    ref=[f.Variable] ':' distribution=Distribution
;

Distribution:
    UniformDistribution | DiscreteDistribution | NormalDistribution
;

UniformDistribution:
    'uniform' '(' '[' values+=FLOAT[','] ']' ')'
;

DiscreteDistribution:
    'discrete' '(' '['
        pairs += DiscreteValue[',']
    ']' ')'
;

NormalDistribution: 
    'normal' '(' 'mean' '=' mean=FLOAT ',' 'std' '=' std=FLOAT ')'
;

DiscreteValue:
    '(' prob=FLOAT ',' value=FLOAT ')'
;

Import: 'import' importURI=STRING;
FQN: ID+['.'];

/* ------------------------------------------ */
// special rule for comments.
Comment:
    /\/\/.*$/
;